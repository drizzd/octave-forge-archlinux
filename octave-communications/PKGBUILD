# Maintainer: Clemens Buchacher <drizzd@aon.at>
_pack=communications
pkgname=octave-$_pack
pkgver=1.1.0
pkgrel=2
pkgdesc="Digital Communications, Error Correcting Codes (Channel Code), Source Code functions, Modulation and Galois Fields"
arch=(any)
url="http://octave.sourceforge.net/$_pack/"
license=('GPL')
groups=('octave-forge')
depends=('octave>=3.4.0' 'octave-signal>=1.0.0' 'octave-image')
makedepends=()
optdepends=()
backup=()
options=()
install=$pkgname.install
_archive=$_pack-$pkgver.tar.gz
source=("http://downloads.sourceforge.net/octave/$_pack-$pkgver.tar.gz"
	resolve-class-LU.patch)
noextract=("$_archive")
md5sums=('1ec83d2757d5aa7d65be4a4c29741eba'
         '7a42034d78c4945e0db0911f15d000d4')

build() {
  cd "$srcdir"

  tar xzf $_archive
  (
    cd $_pack
    patch -p1 <"$srcdir/resolve-class-LU.patch"
  )
  rm -f $_archive
  tar czf $_archive $_pack

  mkdir -p builddir
  octave -q -f --eval "pkg build -nodeps builddir $_archive"
}

package() {
  mkdir -p "$pkgdir/usr/share/octave/packages"
  mkdir -p "$pkgdir/usr/lib/octave/packages"
  cp "$srcdir/builddir/$_archive" "$pkgdir/usr/share/octave/$_pack.tar.gz"
}
