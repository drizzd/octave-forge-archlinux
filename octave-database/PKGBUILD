#
# Maintainer: Clemens Buchacher <drizzd@aon.at>
#
# You can use the newpkg script from
# https://github.com/drizzd/octave-forge-archlinux to automatically generate
# new octave-forge PKGBUILDs or update existing ones. Patches welcome.
#

_pack=database
pkgname=octave-$_pack
pkgver=2.1.0
pkgrel=1
pkgdesc="Interface to SQL databases, currently only postgresql using libpq."
arch=(any)
url="http://octave.sourceforge.net/$_pack/"
license=('GPL3')
groups=('octave-forge')
depends=('octave>=3.6.2' 'postgresql-libs')
makedepends=()
optdepends=()
backup=()
options=()
install=$pkgname.install
_archive=$_pack-$pkgver.tar.gz
_archive_mod=$_pack-$pkgver-$pkgrel.tar.gz
source=("http://downloads.sourceforge.net/octave/$_archive")
md5sums=('4d5696e32f52e9ce5b3e05573bf057b0')

build() {
  cd "$srcdir"

  packdir=$_pack-$pkgver
  (
    cd "$packdir"
    find src -type f | xargs sed -i "s#\<postgresql/##g"
    sed -i "s/\(AC_INIT.*\)\[2\.0\.0]/\1[$pkgver]/" src/configure.ac
  )
  rm -f "$_archive"
  tar czf "$_archive_mod" "$packdir"

  # pkg build would fail, so we skip that and let pkg install do the build:
  # https://savannah.gnu.org/bugs/index.php?38491
}

package() {
  mkdir -p "$pkgdir/usr/share/octave/packages"
  mkdir -p "$pkgdir/usr/lib/octave/packages"
  cp "$srcdir/$_archive_mod" "$pkgdir/usr/share/octave/$_pack.tar.gz"
}
