#
# Submitter: Dknight
# Contributor: Maksim Sipos <msipos@mailc.net>
# Maintainer: Clemens Buchacher <drizzd@aon.at>
#
# You can use the newpkg script from
# https://github.com/drizzd/octave-forge-archlinux to automatically generate
# new octave-forge PKGBUILDs or update existing ones. Patches welcome.
#

_pack=miscellaneous
pkgname=octave-$_pack
pkgver=1.2.1
pkgrel=1
pkgdesc="Miscellaneous tools that don't fit somewhere else."
arch=(any)
url="http://octave.sourceforge.net/$_pack/"
license=('GPL3')
groups=('octave-forge')
depends=('octave>=3.6.0' 'octave-general>=1.3.1')
makedepends=('ncurses' 'units')
optdepends=()
backup=()
options=()
install=$pkgname.install
source=("hg+http://hg.code.sf.net/p/octave/$_pack")
md5sums=('SKIP')

_get_revision() {
  revision=$pkgver
  if ! [ -n "$PKGVER_KEEP" ]
  then
    tag=$(hg parents --template '{latesttag}')
    if [ "$tag" != null ]
    then
      revision="$tag"
    fi
  fi
  printf "%s" "$revision"
}

_install_dir() {
  src=$1
  dst=$2
  mkdir -p "$(dirname "$dst")"
  cp -rT "$src" "$dst"
}

pkgver() {
  cd "$_pack"
  _get_revision
}

prepare() {
  cd "$_pack"
  revision=$pkgver
  if [ "$revision" = "1.2.1" ]
  then
    # This is the revision which updates the Version field in DESCRIPTION
    revision="389:ea20229693a1"
  fi
  hg update "$revision"
  hg archive --exclude '.hg*' "$srcdir/$_pack.tar.gz"
}

build() {
  cd "$srcdir"
  mkdir -p builddir
  octave -q -f --eval "pkg build -verbose -nodeps builddir $_pack.tar.gz"
}

package() {
  mkdir -p "$pkgdir/usr/share/octave/packages"
  mkdir -p "$pkgdir/usr/lib/octave/packages"
  cp "$srcdir"/builddir/$_pack-$pkgver.tar.gz "$pkgdir/usr/share/octave/$_pack.tar.gz"
}
