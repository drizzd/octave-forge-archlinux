#
# Maintainer: Clemens Buchacher <drizzd@aon.at>
#
# You can use the newpkg script from
# https://github.com/drizzd/octave-forge-archlinux to automatically generate
# new octave-forge PKGBUILDs or update existing ones. Patches welcome.
#

_pack=linear-algebra
pkgname=octave-$_pack
pkgver=2.2.1
pkgrel=1
pkgdesc="Additional linear algebra code, including general SVD and matrix functions."
arch=(any)
url="http://octave.sourceforge.net/$_pack/"
license=('custom')
groups=('octave-forge')
depends=('octave>=3.2.3' 'octave-general>=1.3.0')
makedepends=()
optdepends=()
backup=()
options=()
install=$pkgname.install
source=("hg+http://hg.code.sf.net/p/octave/$_pack")
md5sums=('SKIP')

_install_dir() {
  src=$1
  dst=$2
  mkdir -p "$(dirname "$dst")"
  cp -rT "$src" "$dst"
}

prepare() {
  cd "$_pack"
  revision=$pkgver
  # This is the revision which updates DESCRIPTION to 2.2.1.
  if [ "$revision" = "2.2.1" ]
  then
    # This is the revision which updates the Version field in DESCRIPTION
    revision="170:04394cbb2cd1"
  fi
  hg update "$revision"
  hg archive --exclude '.hg*' "$srcdir/$_pack.tar.gz"
}

build() {
  cd "$srcdir"
  mkdir -p builddir
  octave -q -f --eval "pkg build -verbose -nodeps builddir $_pack.tar.gz"
}

package() {
  mkdir -p "$pkgdir/usr/share/octave/packages"
  mkdir -p "$pkgdir/usr/lib/octave/packages"
  cp "$srcdir"/builddir/$_pack-$pkgver.tar.gz "$pkgdir/usr/share/octave/$_pack.tar.gz"
}
